# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Arjan Timmerman <arjan@archlinux.org>
# Contributor: Wael Nasreddine <gandalf@siemens-mobiles.org>
# Contributor: Tor Krill <tor@krill.nu>
# Contributor: Will Rea <sillywilly@gmail.com>

pkgbase=network-manager-applet
pkgname=(network-manager-applet nm-connection-editor)
pkgdesc="Applet for managing network connections"
url="https://wiki.gnome.org/Projects/NetworkManager/"
pkgver=1.8.14
pkgrel=1
arch=(x86_64)
license=(GPL LGPL)
depends=(libnm-glib libgudev gtk3 mobile-broadband-provider-info iso-codes gcr)
makedepends=(libsecret libnotify libmm-glib intltool gobject-introspection git gtk-doc meson)
options=(!emptydirs)
_commit=c5258d7245985925eaeefeb62d2a3548b4208455  # tags/1.8.14^0
source=("git+https://gitlab.gnome.org/GNOME/network-manager-applet.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-dev/dev/;s/-/+/g'
}

prepare() {
  cd $pkgbase
}

build() {
  arch-meson $pkgbase build -D selinux=false
  ninja -C build
}

check() {
  cd build
  meson test
}

_pick() {
  local f; for f; do
    local s="$pkgdir/$f" d="$srcdir/nce/$f"
    mkdir -p "$(dirname "$d")"
    mv "$s" "$d"
  done
}

package_network-manager-applet() {
  depends=(nm-connection-editor libmm-glib libnotify libsecret networkmanager)

  DESTDIR="$pkgdir" ninja -C build install

### Split nm-connection-editor
  _pick usr/bin/nm-connection-editor
  _pick usr/{include,lib}
  _pick usr/share/{gir-1.0,glib-2.0,gtk-doc,locale,metainfo}
  _pick usr/share/applications/nm-connection-editor.desktop
  _pick usr/share/icons/hicolor/22x22/apps/nm-device-wwan.png
  _pick usr/share/man/man1/nm-connection-editor.1
}

package_nm-connection-editor() {
  pkgdesc="NetworkManager GUI connection editor and widgets"
  provides=("libnm-gtk=$pkgver-$pkgrel" "libnma=$pkgver-$pkgrel")
  conflicts=(libnm-gtk)
  replaces=(libnm-gtk)

  mv nce/* "$pkgdir"
}
