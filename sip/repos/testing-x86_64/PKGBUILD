# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Contributor: riai <riai@bigfoot.com>, Ben <ben@benmazer.net>

pkgbase=sip
pkgname=('sip' 'python-sip' 'python2-sip')
pkgver=4.19.10
pkgrel=1
arch=('x86_64')
url='http://www.riverbankcomputing.com/software/sip/intro'
license=('custom:"sip"')
makedepends=('python' 'python2')
source=("http://sourceforge.net/projects/pyqt/files/sip/sip-$pkgver/sip-$pkgver.tar.gz")
sha256sums=('31ac7d48be8fd2df5f3bdb712f66a7d9a858cc1f6cb35fc95d829e0a69c3dd48')

prepare() {
  cp -a sip-$pkgver{,-py2}
}

build() {
  cd "$srcdir"/sip-$pkgver
  python configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS"
  make

  cd "$srcdir"/sip-$pkgver-py2
  python2 configure.py CFLAGS="$CFLAGS" LFLAGS="$LDFLAGS"
  make
}

package_sip() {
  pkgdesc="A tool that makes it easy to create Python bindings for C and C++ libraries"
  depends=('glibc')

  cd sip-$pkgver
  make DESTDIR="$pkgdir" install -C sipgen

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python-sip() {
  pkgdesc="Python 3.x SIP bindings for C and C++ libraries"
  depends=('python')

  cd sip-$pkgver
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 sipconfig.py "$pkgdir"/usr/lib/python3.6/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "$pkgdir"/usr/lib/python3.6/site-packages/sipdistutils.py

  mkdir -p "$pkgdir"/usr/lib/python3.6/site-packages/PyQt5
  ln -sr "$pkgdir"/usr/lib/python3.6/site-packages/sip.* "$pkgdir"/usr/lib/python3.6/site-packages/PyQt5

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}

package_python2-sip() {
  pkgdesc="Python 2.x SIP bindings for C and C++ libraries"
  depends=('python2')

  cd sip-$pkgver-py2
  make DESTDIR="$pkgdir" install -C siplib

  install -Dm644 sipconfig.py "$pkgdir"/usr/lib/python2.7/site-packages/sipconfig.py
  install -Dm644 sipdistutils.py "$pkgdir"/usr/lib/python2.7/site-packages/sipdistutils.py

  mkdir -p "$pkgdir"/usr/lib/python2.7/site-packages/PyQt5
  ln -sr "$pkgdir"/usr/lib/python2.7/site-packages/sip.* "$pkgdir"/usr/lib/python2.7/site-packages/PyQt5

  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
