# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt5-webengine
_qtver=5.11.0
pkgver=${_qtver/-/}
pkgrel=3
arch=('x86_64')
url='http://qt-project.org/'
license=('LGPL3' 'LGPL2.1' 'BSD')
pkgdesc='Provides support for web applications using the Chromium browser project'
depends=('qt5-webchannel' 'qt5-location' 'libxcomposite' 'libxrandr' 'pciutils' 'libxss' 
         'libevent' 'snappy' 'nss' 'protobuf' 'libxslt' 'minizip' 'ffmpeg' 're2' 'libvpx')
makedepends=('python2' 'git' 'gperf' 'jsoncpp' 'ninja' 'qt5-tools')
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-everywhere-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
         qtwebengine-harmony.patch qtwebengine-gcc8.patch qtwebengine-ffmpeg4.patch
         falkon-copy-menu.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=af0c47b1"
         qtwebengine-wayland1.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=9397251b"
         qtwebengine-wayland2.patch::"http://code.qt.io/cgit/qt/qtwebengine.git/patch/?id=a66d4cd8")
sha256sums=('5dd754d603c66d36e93b96b4f7c24a6e6269ae6a1682a524b8baa664d5c44b45'
            'feca54ab09ac0fc9d0626770a6b899a6ac5a12173c7d0c1005bc3964ec83e7b3'
            '7f84891ef1ac9b7943ac5b92f2df8caf0c24c3695bdc6296926b581a87c0e856'
            '4a831d89fb0d6a6ced23115ced71e60513ce279fba4bd493178842647948f510'
            'b5b3d873c51ed2ad05b2ffe1f3d14cd95a732f0e1288c04a502d65576e316f5d'
            '6a31607401f63eb6aec5e131c5f7a4723af21615a2df720d148e253ca301894a'
            '6baeb241165a1fd94ada95b27924ba55f74761b1b9d406d73ebcce4b139abf60')

prepare() {
  mkdir -p build

  # Hack to force using python2
  mkdir -p bin
  ln -s /usr/bin/python2 bin/python

  cd ${_pkgfqn}

  # FreeType 2.8.1
  patch -Np1 -i ../qtwebengine-harmony.patch
  # Fix copy context menu
  patch -p1 -i ../falkon-copy-menu.patch
  # Fix openGL detection on Wayland
  patch -p1 -i ../qtwebengine-wayland1.patch
  patch -p1 -i ../qtwebengine-wayland2.patch
  # Fix build with gcc8 (Fedora)
  cd src/3rdparty/chromium
  patch -p1 -i "$srcdir"/qtwebengine-gcc8.patch
  # Fix build with ffmpeg 4
  patch -p2 -i "$srcdir"/qtwebengine-ffmpeg4.patch
}

build() {
  cd build

  export PATH="$srcdir/bin:$PATH"
  qmake ../${_pkgfqn} -- \
    -proprietary-codecs \
    -system-ffmpeg \
    -webp \
    -spellchecker \
    -webengine-icu
  make
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;

  install -Dm644 "$srcdir"/${_pkgfqn}/src/3rdparty/chromium/LICENSE "$pkgdir"/usr/share/licenses/${pkgname}/LICENSE.chromium
}
