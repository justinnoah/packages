# $Id$
# Maintainer: Thomas BÃ¤chler <thomas@archlinux.org>

pkgname=intel-ucode
pkgver=20180703
# Some random "download id" that intel has in their downloadcenter
_dlid=27945
pkgrel=1
pkgdesc='Microcode update files for Intel CPUs'
arch=('any')
url='https://downloadcenter.intel.com/SearchResult.aspx?lang=eng&keyword=processor%20microcode%20data%20file'
replaces=('microcode_ctl')
makedepends=('iucode-tool')
license=('custom')
source=("https://downloadmirror.intel.com/${_dlid}/eng/microcode-${pkgver}.tgz"
        'LICENSE')
sha256sums=('4a1a346fdf48e1626d4c9d0d47bbbc6a4052f56e359c85a3dd2d10fd555e5938'
            '6983e83ec10c6467fb9101ea496e0443f0574c805907155118e2c9f0bbea97b6')

build() {
  cd "$srcdir"

  rm -f intel-ucode{,-with-caveats}/list
  mkdir -p kernel/x86/microcode
  iucode_tool -w kernel/x86/microcode/GenuineIntel.bin intel-ucode{,-with-caveats}/
  echo kernel/x86/microcode/GenuineIntel.bin | bsdcpio -o -H newc -R 0:0 > intel-ucode.img
}

package() {
  cd "$srcdir"

  install -D -m0644 intel-ucode.img "${pkgdir}"/boot/intel-ucode.img
  install -D -m0644 LICENSE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
