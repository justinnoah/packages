# Maintainer: artoo <artoo@artixlinux.org>
# Contributor: williamh <williamh@gentoo.org>

_url="https://github.com"

_net_url="${_url}/gentoo/netifrc/archive"
_func_url="${_url}/gentoo/gentoo-functions/archive"

_func_ver=0.12

pkgname=netifrc
pkgver=0.6.1
pkgrel=1
pkgdesc="Gentoo Network Interface Management Scripts"
arch=('x86_64')
url="http://www.gentoo.org/proj/en/base/openrc/"
license=('BSD2')
depends=('eudev-openrc')
backup=('etc/conf.d/net')
source=("${pkgname}-${pkgver}.tar.gz::${_net_url}/${pkgver}.tar.gz"
        "gentoo-functions-${_func_ver}.tar.gz::${_func_url}/${_func_ver}.tar.gz")
sha256sums=('004907e0c3db2d106d6a51d604d79f971f1013fa7642054ad7efe5076e52f7b3'
            '4a6ddb1d8be7b52aae68761fb26546663b6b0e74b155df263cddfbfa72373965')

_id=artix
if [ -f /usr/lib/os-release ]; then
    . /usr/lib/os-release
    _id=$ID
fi
_net_args=(SYSCONFDIR=/etc PREFIX=/usr SBINDIR=/usr/bin LIBEXECDIR=/usr/lib/${pkgname})
_func_args=(ROOTPREFIX=/usr ROOTSBINDIR=/usr/bin ROOTLIBEXECDIR=/usr/lib/$_id)

build(){
    # make netifrc
    cd "${srcdir}/${pkgname}-${pkgver}"
    make "${_net_args[@]}"
    cd ${srcdir}/gentoo-functions-${_func_ver}
    make "${_func_args[@]}"
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    make DESTDIR="${pkgdir}" "${_net_args[@]}" install

    install -Dm 644 "${srcdir}/${pkgname}-${pkgver}/doc/net.example" "${pkgdir}/etc/conf.d/net"

    install -d "${pkgdir}/etc/runlevels/boot"
    ln -sf "/etc/init.d/net.lo" "${pkgdir}/etc/runlevels/boot/net.lo"

    cd ${srcdir}/gentoo-functions-${_func_ver}
    make DESTDIR="${pkgdir}" "${_func_args[@]}" install

    sed -e "s|/lib/gentoo/functions.sh|/usr/lib/$_id/functions.sh|g" \
        -i "${pkgdir}/usr/lib/${pkgname}/sh/functions.sh"

    install -Dm644 ${srcdir}/${pkgname}-${pkgver}/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
