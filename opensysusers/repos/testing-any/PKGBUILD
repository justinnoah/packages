# Maintainer: Chris Cromer <cromer@artixlinux.org>
# Maintainer: artoo <artoo@artixlinux.org>

pkgbase=opensysusers
pkgname=(opensysusers opensysusers-openrc opensysusers-docs)
pkgver=0.4.7
pkgrel=2
pkgdesc="A standalone utility for handling systemd-style sysusers.d users and groups"
arch=('any')
url="https://github.com/artix-linux/opensysusers"
license=('BSD2')
makedepends=('docbook-xsl')
source=("${pkgbase}-${pkgver}.tar.gz::https://github.com/artix-linux/opensysusers/archive/${pkgver}.tar.gz"
		'opensysusers.hook'
		'sysusers-hook'
		"no-rlvl.patch::https://github.com/artix-linux/opensysusers/commit/4071c407ebb53b2a08d4093915561eaf74839d20.patch")
sha256sums=('b69462ecd492270e46d93c0fa564a6f530dad25a09fc9a760998584affe15866'
            'cefc133d78851cc3838eec273aeb127814c955be21c6e3fc0f9ba4391e534ec2'
            '4222b2496d8bb1ac9c07707e4ef1b334b8b59eb15451ea8ba0fd6e4c93e50c56'
            'a24ff1b606ee129fa26b08b83121eeb3dd0321b03e4a645ca99b580f5e4228c5')

prepare(){
    cd "${pkgbase}-${pkgver}"
    patch -Np 1 -i ${srcdir}/no-rlvl.patch
}

build(){
    cd "${pkgbase}-${pkgver}"
    make PREFIX=/usr HAVEOPENRC=yes
}

package_opensysusers() {
    depends=('pacman')
    optdepends=('opensysusers-openrc: Opensysusers init script')

    cd "${pkgbase}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-shared install-default-bin

    # pacman hooks
    install -Dm755 ${srcdir}/sysusers-hook "$pkgdir"/usr/share/libalpm/scripts/sysusers-hook
    install -Dm644 -t "$pkgdir"/usr/share/libalpm/hooks ${srcdir}/*.hook

    ln -snf "/usr/bin/sysusers" "${pkgdir}/usr/bin/systemd-sysusers"
}

package_opensysusers-openrc(){
    pkgdesc="OpenRC opensysusers init script"
    depends=('opensysusers')

    cd "${pkgbase}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-openrc

    sed -e 's|#!/sbin/openrc-run|#!/usr/bin/openrc-run|g' \
            -i ${pkgdir}/etc/init.d/opensysusers

    install -dm755 ${pkgdir}/etc/runlevels/boot
    ln -snf "/etc/init.d/opensysusers" "${pkgdir}/etc/runlevels/boot/opensysusers"
}

package_opensysusers-docs(){
    pkgdesc="Opensysusers man"
    depends=('opensysusers')

    cd "${pkgbase}-${pkgver}"
    make PREFIX=/usr DESTDIR="${pkgdir}" install-man
}
